plugins {
    id 'com.wgsoft.game.clanomania.common-conventions'
    id 'com.android.application'
}

configurations {
    natives
}

ext {
    androidVersion = 31
    architectures = ['armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64']
    jniLibsDir = 'libs'
}

dependencies {
    implementation project(':core')
    implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    architectures.each {
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-$it"
    }
}

android {
    buildToolsVersion "31.0.0"
    compileSdkVersion androidVersion
    namespace 'com.wgsoft.game.clanomania'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    sourceSets {
        main {
            assets.srcDirs = [assetsDir]
            jniLibs.srcDirs = [jniLibsDir]
        }
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion androidVersion
        versionCode 1
        versionName version
    }
}

task copyAndroidNatives {
    doFirst {
        architectures.each {
            file("$jniLibsDir/$it").mkdirs()
        }

        configurations.natives.files.each { jar ->
            def outputDir = null

            architectures.each {
                if(jar.name.endsWith("natives-${it}.jar")) {
                    outputDir = file("$jniLibsDir/$it")
                }
            }

            if(outputDir) {
                copy {
                    from zipTree(jar)
                    into outputDir
                    include '*.so'
                }
            }
        }
    }
}

tasks.whenTaskAdded {
    if(it.name.contains('package')) {
        it.dependsOn copyAndroidNatives
    }
}
